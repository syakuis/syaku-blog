buildscript {
  ext {
    springBootVersion = "2.1.2.RELEASE"
    lombokVersion = "1.18.2"
    restDocsVersion = "2.0.3.RELEASE"
  }

  repositories {
    maven {
      url "https://plugins.gradle.org/m2/"
    }
    mavenCentral()
    jcenter()
  }

  dependencies {
    classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
    classpath "org.asciidoctor:asciidoctor-gradle-plugin:1.5.3"
  }
}

apply plugin: "java"
apply plugin: "idea"
apply plugin: "org.springframework.boot"
apply plugin: "io.spring.dependency-management"
apply plugin: "org.asciidoctor.convert"

idea {
  module {
    inheritOutputDirs = true
    outputDir = compileJava.destinationDir
    testOutputDir = compileTestJava.destinationDir
  }
}

wrapper {
  gradleVersion = '5.4'
}

group "org.syaku"
version "1.0.0.BUILD-SNAPSHOT"

sourceCompatibility = 1.8
targetCompatibility = 1.8

ext {
  snippetsDir = file("build/generated-snippets")
}

repositories {
  jcenter()
  mavenCentral()
}

compileJava.dependsOn(processResources)

configurations {
  compile.exclude module: "spring-boot-starter-tomcat"
}

dependencies {
  implementation "org.springframework.boot:spring-boot-starter-web"
  implementation "org.springframework.boot:spring-boot-starter-webflux"
  testImplementation "org.springframework.boot:spring-boot-starter-test"
  compileOnly "org.springframework.boot:spring-boot-configuration-processor"

  compile "org.springframework.boot:spring-boot-starter-validation"

  compile "org.springframework.boot:spring-boot-starter-jetty"

  testCompile "junit:junit:4.12"
  testCompile "org.jmock:jmock-junit4:2.8.2"
  testCompile "org.mockito:mockito-core:2.+"

  compileOnly "org.projectlombok:lombok:${lombokVersion}"
  annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
  testCompileOnly "org.projectlombok:lombok:${lombokVersion}"
  testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"

  compile "org.springframework.boot:spring-boot-starter-data-jpa"

  compileOnly "com.h2database:h2:1.4.191"
  testCompile "com.h2database:h2:1.4.191"

  compile "org.springframework.boot:spring-boot-starter-security"
  testCompile "org.springframework.security:spring-security-test"

  asciidoctor "org.springframework.restdocs:spring-restdocs-asciidoctor:${restDocsVersion}"
  testCompile "org.springframework.restdocs:spring-restdocs-mockmvc:${restDocsVersion}"
}

test {
  outputs.dir snippetsDir
}

asciidoctor {
  inputs.dir snippetsDir
  dependsOn test
}

bootJar {
  enabled = true
  mainClassName = "org.syaku.blog.Application"
  dependsOn asciidoctor
  from ("${asciidoctor.outputDir}/html5") {
    into 'static/docs'
  }
  launchScript()
}
